apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    project.archivesBaseName = "Hongjinyoung"
    defaultConfig {
        applicationId "com.chsapps.yt_hongjinyoung"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        ndk {
            abiFilters 'x86','armeabi-v7a'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    signingConfigs {
        releaseWith {
            storeFile file(keystoreProperties['keystore'])
            storePassword keystoreProperties['keystore_pass']
            keyAlias keystoreProperties['key_alias']
            keyPassword keystoreProperties['key_pass']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            signingConfig signingConfigs.releaseWith
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles fileTree(include: ['*.pro'], dir: '../proguards').asList().toArray()
            buildConfigField("String", "API_BASE_URL", '"https://api-idol-dot-calllocker-94b36.appspot.com/"')
        }
        debug{
            debuggable true
            versionNameSuffix "-beta"
            buildConfigField("String", "API_BASE_URL", '"https://api-idol-test-dot-calllocker-94b36.appspot.com/"')
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-v${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}

configurations.all{
    resolutionStrategy.force "com.android.support:animated-vector-drawable:$rootProject.ext.appCompatVersion"
    resolutionStrategy.force "com.android.support:support-media-compat:$rootProject.ext.appCompatVersion"
    resolutionStrategy.force "com.android.support:support-v4:$rootProject.ext.appCompatVersion"
    resolutionStrategy.force "com.android.support:customtabs:$rootProject.ext.appCompatVersion"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test:runner:1.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "com.google.ads.mediation:facebook:$rootProject.ext.mediationFacebookVersion"
    implementation "com.google.android.gms:play-services-ads:$rootProject.ext.playservicesAdsVersion"
    implementation "com.google.android.ads:mediation-test-suite:$rootProject.ext.mediationTestSuiteVersion"

    implementation "com.facebook.android:facebook-core:4.39.0"
    implementation 'com.facebook.stetho:stetho:1.5.0'

    implementation 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'

    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'

    implementation 'org.greenrobot:eventbus:3.0.0'

    implementation 'com.scottyab:secure-preferences-lib:0.1.4'
    implementation 'com.facebook.android:facebook-android-sdk:4.39.0'
    implementation 'com.facebook.android:audience-network-sdk:5.1.0'
    implementation 'pub.devrel:easypermissions:2.0.0'

    implementation 'com.airbnb.android:lottie:2.5.5'
    implementation 'com.nick.mowen.linkpreview:linkpreview:1.1.3'

    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    implementation 'com.squareup.sqlbrite:sqlbrite:1.1.1'

    implementation "com.crashlytics.sdk.android:crashlytics:2.9.8"
}

apply plugin: 'com.google.gms.google-services'